#
# Example of output for version code 3.3.00:
#   PROJECT_VERSION = 3.3.00
#   RKH_VERSION_CODE = 3300
#   RKH_VERSION_STRING = "3.3.00"
#   RKH_VERSION_MAJOR = 3
#   RKH_VERSION_MINOR = 3
#   RKH_VERSION_PATCHLEVEL = 00
#

file(READ ${RKH_BASE}/VERSION ver)

string(REGEX MATCH "VERSION_MAJOR = ([0-9]*)" _ ${ver})
set(PROJECT_VERSION_MAJOR ${CMAKE_MATCH_1})

string(REGEX MATCH "VERSION_MINOR = ([0-9]*)" _ ${ver})
set(PROJECT_VERSION_MINOR ${CMAKE_MATCH_1})

string(REGEX MATCH "PATCHLEVEL = ([0-9]*)" _ ${ver})
set(PROJECT_VERSION_PATCH ${CMAKE_MATCH_1})

string(REGEX MATCH "VERSION_TWEAK = ([0-9]*)" _ ${ver})
set(PROJECT_VERSION_TWEAK ${CMAKE_MATCH_1})

string(REGEX MATCH "EXTRAVERSION = ([a-z0-9]*)" _ ${ver})
set(PROJECT_VERSION_EXTRA ${CMAKE_MATCH_1})

# Temporary convenience variable
set(PROJECT_VERSION_WITHOUT_TWEAK ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH})

if(PROJECT_VERSION_EXTRA)
  set(PROJECT_VERSION_EXTRA_STR "-${PROJECT_VERSION_EXTRA}")
endif()

if(PROJECT_VERSION_TWEAK)
  set(PROJECT_VERSION ${PROJECT_VERSION_WITHOUT_TWEAK}.${PROJECT_VERSION_TWEAK})
else()
  set(PROJECT_VERSION ${PROJECT_VERSION_WITHOUT_TWEAK})
endif()

set(PROJECT_VERSION_STR ${PROJECT_VERSION}${PROJECT_VERSION_EXTRA_STR})

if (NOT NO_PRINT_VERSION)
  message(STATUS "RKH version: ${PROJECT_VERSION_STR} (${RKH_BASE})")
endif()

set(RKH_VERSION_MAJOR ${PROJECT_VERSION_MAJOR})
set(RKH_VERSION_MINOR ${PROJECT_VERSION_MINOR})
set(RKH_VERSION_PATCHLEVEL ${PROJECT_VERSION_PATCH})

if(PROJECT_VERSION_EXTRA)
  set(RKH_VERSION_STRING "\"${PROJECT_VERSION_WITHOUT_TWEAK}-${PROJECT_VERSION_EXTRA}\"")
else()
  set(RKH_VERSION_STRING "\"${PROJECT_VERSION_WITHOUT_TWEAK}\"")
endif()

set(RKH_VERSION_CODE "${RKH_VERSION_MAJOR}${RKH_VERSION_MINOR}${RKH_VERSION_PATCHLEVEL}")

# Cleanup convenience variables
unset(PROJECT_VERSION_WITHOUT_TWEAK)
